import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'org.beryx.jlink' version '2.24.4'
    id 'org.jreleaser' version '1.0.0'
}

version file('version').text
def canonicalVersion = file('canonical_version').text

apply from: 'gradle/jreleaser.gradle'
apply from: 'gradle/java.gradle'

clean {
    setDelete(Set.of())
    doLast {
        fileTree(dir: project.buildDir).exclude("jreleaser/**").visit { FileVisitDetails details ->
            delete details.file
        }
    }
}

if (!file("${projectDir}/pdxu.properties").exists()) {
    file("${projectDir}/pdxu.properties").write(file("${projectDir}/misc/pdxu.properties.default").getText())
}

dependencies {
    implementation project(':pdxu-editor')
    implementation project(':pdxu-app')
    implementation project(':pdxu-io')
    implementation project(':pdxu-model')
}

repositories {
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        url = 'https://maven.nikr.net/'
    }
    maven { url "https://jitpack.io" }
}

List<String> runArgs = [
        "--add-exports", "javafx.graphics/com.sun.javafx.scene=com.jfoenix",
        "--add-exports", "javafx.graphics/com.sun.javafx.stage=com.jfoenix",
        "--add-exports", "javafx.base/com.sun.javafx.binding=com.jfoenix",
        "--add-exports", "javafx.base/com.sun.javafx.event=com.jfoenix",
        "--add-exports", "javafx.controls/com.sun.javafx.scene.control=com.jfoenix",
        "--add-exports", "javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix",
        "--add-opens", "com.google.gson/com.google.gson.internal=io.sentry",
        "--add-opens", "java.base/java.lang.reflect=com.jfoenix",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model=com.fasterxml.jackson.databind",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model.eu4=com.fasterxml.jackson.databind",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model.ck3=com.fasterxml.jackson.databind",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model.hoi4=com.fasterxml.jackson.databind",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model.stellaris=com.fasterxml.jackson.databind",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model.ck2=com.fasterxml.jackson.databind",
        "--add-opens", "com.crschnick.pdxu.model/com.crschnick.pdxu.model.vic2=com.fasterxml.jackson.databind",
        "-Xmx4g",
        "-XX:+ExitOnOutOfMemoryError"]

application {
    mainModule = 'com.crschnick.pdxu.app'
    mainClass = 'com.crschnick.pdxu.app.Main'
    applicationDefaultJvmArgs = runArgs + ["-ea"]
}




jlink {
    imageDir = file("$buildDir/image")
    options = [
            // '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages']

    launcher {
        moduleName = 'com.crschnick.pdxu.app'
        mainClassName = 'com.crschnick.pdxu.app.Main'
        name = 'pdxu'
        jvmArgs = runArgs
    }

    customImage {
        appModules = [
                'com.crschnick.pdxu.app',
                'com.crschnick.pdxu.editor',
                'org.slf4j',
                'org.slf4j.simple',
                'jdk.crypto.ec',
                'org.kordamp.ikonli.materialdesign',
                'jdk.zipfs',
                'jdk.accessibility'
        ]
    }

    jpackage {
        appVersion = canonicalVersion
        imageOutputDir = file("$buildDir/dist")
        imageName = 'Pdx-Unlimiter'
        if (DefaultNativePlatform.currentOperatingSystem.windows) {
            icon = "misc/logo.ico"
        } else if (DefaultNativePlatform.currentOperatingSystem.linux) {
            icon = "misc/logo.png"
        } else {
            icon = "misc/logo.icns"
        }
        skipInstaller = true
        applicationName = 'Pdx-Unlimiter'
    }
}

def getDistDir() {
    if (DefaultNativePlatform.currentOperatingSystem.macOsX) {
        return "${buildDir}/dist/Pdx-Unlimiter.app/"
    } else {
        return "${buildDir}/dist/Pdx-Unlimiter/"
    }
}

def getResourcesDir() {
    if (DefaultNativePlatform.currentOperatingSystem.macOsX) {
        return "${getDistDir()}/Contents/Resources"
    } else {
        return getDistDir()
    }
}

task writeVersion(type: DefaultTask) {
    doLast {
        file(getResourcesDir() + '/version').write(String.valueOf(version))
    }
}

task copyResources(type: Copy) {
    into getResourcesDir() + 'resources'
    from "${projectDir}/resources"
}

task copyLang(type: Copy) {
    into getResourcesDir() + 'lang'
    from "${projectDir}/lang"
}

task copyStartScripts(type: Copy) {
    into getResourcesDir() + 'bin'
    if (DefaultNativePlatform.currentOperatingSystem.windows) {
        from "${projectDir}/misc/pdxu.bat"
    } else if (DefaultNativePlatform.currentOperatingSystem.linux) {
        from "${projectDir}/misc/pdxu"
    } else {

    }
}

jpackage.finalizedBy(writeVersion, copyResources, copyLang, copyStartScripts)

task createDist(type: Zip, dependsOn: jpackage) {
    destinationDirectory = buildDir
    archivesBaseName = "pdx_unlimiter"
    archiveAppendix = DefaultNativePlatform.currentOperatingSystem.windows ? "windows" : DefaultNativePlatform.currentOperatingSystem.linux ? "linux" : "mac"
    archiveVersion = ""
    from getDistDir()
}
